plugins {
    id "java"
    id "java-library"
    id "idea"
    id "com.google.protobuf" version "0.9.4"
    id 'io.finazon.gradleplugin.codegen'
    id "com.vanniktech.maven.publish" version "0.28.0"
}

archivesBaseName = "finazon-grpc-java"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "com.google.protobuf:protobuf-java:${protobufVersion}"
    compileOnly "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation "io.grpc:grpc-auth:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    api "io.grpc:grpc-protobuf:${grpcVersion}"
    api "io.grpc:grpc-services:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation "com.google.auth:google-auth-library-oauth2-http:1.18.0"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.16.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.15.1'
        }
    }
    generateProtoTasks {
        ofSourceSet('main').configureEach {
            plugins {
                grpc {}
            }
        }
    }
}

generateProto {
    finalizedBy("createExtGrpcService")
}

import com.vanniktech.maven.publish. SonatypeHost

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)

    signAllPublications()

    coordinates("${group}", "${archivesBaseName}", "${version}")
    pom {
        name = 'finazon-grpc-java'
        description = 'Finazon gRPC client library for Java'
        url = 'https://github.com/finazon-io/finazon-grpc-java'

        licenses {
            license {
                name = 'MIT License'
                url = 'http://www.opensource.org/licenses/mit-license.php'
            }
        }
        developers {
            developer {
                id = 'Finazon'
                name = 'Finazon'
                email = 'team@finazon.io'
            }
        }
        scm {
            connection='scm:git:git:github.com/finazon-io/finazon-grpc-java.git'
            developerConnection='scm:git:https://github.com/finazon-io/finazon-grpc-java.git'
            url='https://github.com/finazon-io/finazon-grpc-java'
        }
    }

}
